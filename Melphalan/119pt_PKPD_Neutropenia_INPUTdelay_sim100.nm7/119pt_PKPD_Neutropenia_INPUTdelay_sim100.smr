THETA:      THETA1      THETA2      THETA3      THETA4      THETA5      THETA6      THETA7      THETA8      THETA9      THETA10     THETA11     THETA12     THETA13     THETA14     THETA15     THETA16     
ETA:        ETA1        ETA2        ETA3        ETA4        ETA5        ETA6        ETA7        ETA8        
ERR:        EPS1        EPS2        
119pt_PKPD_Neutropenia_INPUTdelay_sim100.lst	*******	NNNN	eval=0 sig=0 sub=118 obs=2785 NM7.3.0 PP7.3.0 



ESTIMATION OMITTED

 Ttot 0:0 Test 0:0 Tcov 0:0 Ttcl 0:0

$PROB MELPHALAN-OSU11055 TWO COMPARTMENT INFUSION PKPD
$INPUT NOTE ID TIME ANC DV AMT RATE MDV DVID CMT WT BSA SEX AGE CRCL FFM HCT P53FOLD IC50 SLC7A5
$DATA ..\PKPD_OSU11055_Neutropenia_lnplus4.csv IGNORE=#

$SUBR ADVAN6 TOL=3

$MODEL 
    COMP=(PKCENTR)
    COMP=(PKPERI)
       COMP=(STEM)
       COMP=(ANC,DEFOBS)
       COMP=(TRANSIT1)
       COMP=(TRANSIT2)
       COMP=(TRANSIT3)
    COMP=(INPUT2)
    
$PK

   TVCL=THETA1*((CRCL/91.94)**THETA14)*((FFM/59.90)**(0.75))*((HCT/32.50)**THETA16)
   TVV1=THETA2*(FFM/59.90)
   TVQ=THETA3*((FFM/59.90)**(0.75))
   TVV2=THETA4*(1+SLC7A5*THETA15)*(FFM/59.90)
   
   CL=TVCL*EXP(ETA1)
   V1=TVV1*EXP(ETA2)
   Q=TVQ*EXP(ETA3)
   V2=TVV2*EXP(ETA1*THETA5)
   
   S1=V1

   K10=CL/V1
   K12=Q/V1
   K21=Q/V2
   
   BASE=THETA6*EXP(ETA4)
   MTT=THETA7*EXP(ETA5)
   K=4/MTT
   POWER1=THETA8
   SLOPE=THETA9*EXP(ETA6)
   KE=LOG(2)/THETA10
   
   IPT=THETA11*EXP(ETA7)

   IP0=THETA12*EXP(ETA8)
 
   A_0(3)=KE*BASE/K
   A_0(4)=BASE
   A_0(5)=KE*BASE/K
   A_0(6)=KE*BASE/K
   A_0(7)=KE*BASE/K  
   A_0(8)=IP0   
   
$DES
CP=A(1)/V1
DRUG=SLOPE*CP
;****************KINETICS****************************
DADT(1)=A(2)*K21-A(1)*K10-A(1)*K12
DADT(2)=A(1)*K12-A(2)*K21
;****************DYNAMICS****************************

FN=(BASE/0.001)**POWER1
IF(A(4).GT.0.001) FN=(BASE/A(4))**POWER1
KIN=0
IF(TIME.GT.72)    KIN=1/IPT

DADT(3)=K*A(3)*(1-DRUG)*FN - K*A(3)  ;STEM
DADT(4)=K*A(7) - KE*A(4) + KIN*A(8)              ;ANC 
DADT(5)=K*A(3) - K*A(5)              ;TRANSIT 1
DADT(6)=K*A(5) - K*A(6)              ;TRANSIT 2
DADT(7)=K*A(6) - K*A(7)               ;TRANSIT 3 
DADT(8)=-KIN*A(8)    ; INPUT2

$ERROR 
   IPRED=F
IF (CMT .EQ. 4) IPRED=LOG(F)+4
   IRES=DV-IPRED
   W=THETA13
   IF(W.EQ.0) W=1
   IWRES=IRES/W

QK=0
QD=0
IF (CMT .EQ. 1) QK=1
IF (CMT .EQ. 4) QD=1


PKY=IPRED * EXP(EPS1)
PDY=IPRED + W*EPS2
 
 Y=QK*PKY+QD*PDY    

$THETA
29.3 FIX ; THETA1
19.6 FIX ; THETA2
28.3 FIX  ; THETA3
20.8 FIX  ; THETA4
1.31 FIX; THETA5
5.2 FIX  ;THETA6
105 FIX     ;THETA7
0.209 FIX       ;THETA8
10.3 FIX         ;THETA9
7 FIX ; THETA10
12.9 FIX; THETA11
19.9 FIX; THETA12
0.624 FIX ; THETA13

0.148 FIX; THETA14
-0.116 FIX; THETA15
0.205 FIX; THETA16


$OMEGA
0.0777 FIX ;ETA1
0.0681 FIX ;ETA2
0.287 FIX ;ETA3
0.111 FIX  ;ETA4
0.00987 FIX ;ETA5
0.0778 FIX  ;ETA6
0 FIX ; ETA7
2.39 FIX; ETA8

$SIGMA
0.0588 FIX ; EPS1
1 FIX  ;EPS2

$SIMULATION (9215690) ONLYSIM SUBPROBLEMS=200

$TABLE ID TIME AMT MDV DVID CMT Y IPRED
ONEHEADER NOPRINT FILE=119pt_pkpd_neutropenia_inputdelay_sim100.fit
